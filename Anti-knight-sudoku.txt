 % Anti - knight Sudoku 
 % Cells that are a chess knight’s move away from each other cannot hold equal values
 
 num(1..9).
border(1;4;7).

1 {a(R,C,N) : num(N)} 1 :- num(R;C).
1 {a(R,C,N) : num(R)} 1 :- num(C;N).
1 {a(R,C,N) : num(C)} 1 :- num(R;N).
:- a(R,C,N), a(R1,C1,N), R!=R1, C!=C1,(((R-1)/3)*3 + (C-1)/3) == (((R1-1)/3)*3 + (C1-1)/3).

% Anti-knight Conditions
:- a(R,C,N), a(R-2,C-1,N).
:- a(R,C,N), a(R-2,C+1,N).
:- a(R,C,N), a(R-1,C-2,N).
:- a(R,C,N), a(R-1,C+2,N).
:- a(R,C,N), a(R+1,C-2,N).
:- a(R,C,N), a(R+1,C+2,N).
:- a(R,C,N), a(R+2,C-1,N).
:- a(R,C,N), a(R+2,C+1,N).


% Given Numerical Clues
a(1,1,9). % At row (1,1) position, number 9 is placed  
a(1,5,8).
a(1,7,3).
a(1,7,3).
a(2,4,2).
a(2,5,5).
a(2,7,7).
a(3,2,2).
a(3,4,3).
a(3,9,4).
a(4,2,9).
a(4,3,4).
a(5,4,7).
a(5,5,3).
a(5,7,5).
a(5,8,6).
a(6,1,7).
a(6,3,5).
a(6,5,6).
a(6,7,4).
a(7,3,7).
a(7,4,8).
a(7,6,3).
a(7,7,9).
a(8,3,1).
a(8,9,3).
a(9,1,3).
a(9,9,2).


